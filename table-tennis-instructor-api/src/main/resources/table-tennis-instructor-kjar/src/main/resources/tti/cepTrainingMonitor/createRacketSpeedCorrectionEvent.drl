//created on: May 5, 2020
package cepTrainingMonitor

//list any import classes here.
import tableTennisInstructor.model.drools.events.*;
import tableTennisInstructor.model.drools.facts.training.*;
import java.util.ArrayList;
import java.lang.Long;


rule "Create RacketSpeedCorrectionEvent with correction message when 2 or more badRacketSpeed with same speed occurs"
	salience 100
    when
    	// uzmes tek kreirani event i pogledas 6 sek u proslos da li ima jos koji istog ugla
    	//
    	$e1: BadRacketSpeedEvent(processed == false, $speed: speed)
        $e2:  BadRacketSpeedEvent(
        					 (this != $e1) &&
        					 $e1.trainingExecutionId == trainingExecutionId &&
        					 ((speed > 0  && $speed > 0)|| (speed < 0  && $speed < 0 )) &&
        					 (processed == false),
        					 $e1 before [0s, 6s] this
        					)
    then
    	$e1.setProcessed(true);
    	$e2.setProcessed(true);
    	update($e1);
    	update($e2);
        RacketSpeedCorrectionEvent raketCorrectionEvent = new RacketSpeedCorrectionEvent($e1.trainingExecutionId);
       	raketCorrectionEvent.setMessage($e1.speed > 0 ? "Laganije izvoditi pokret" : "Sporije izvoditi pokret");
		insert(raketCorrectionEvent);
        System.out.println("Kreiran event za korekciju brzine reketa");
end


rule "Create RacketSpeedCorrectionEvent with correction message when 2 or more badRacketSpeed with different speed occurs"
	salience 90
    when
    	$e1: BadRacketSpeedEvent(processed == false)
        $e2: BadRacketSpeedEvent (
        					 (this != $e1),
        					 $e1.trainingExecutionId == trainingExecutionId &&
        					 processed == false,
        					 $e1 before [0s,6s] this
        					)

    then
    	$e1.setProcessed(true);
        $e2.setProcessed(true);
        update($e1);
        update($e2);
        RacketSpeedCorrectionEvent raketCorrectionEvent = new RacketSpeedCorrectionEvent($e1.trainingExecutionId);
       	raketCorrectionEvent.setMessage("Brzina je bila i veca i manja od predvidjene, ustalite pokret");
		insert(raketCorrectionEvent);
        System.out.println("Kreiran event za korekciju brzine reketa bez saveta");
end